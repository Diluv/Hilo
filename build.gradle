plugins {
    id 'java'
    id 'com.google.cloud.tools.jib' version '2.1.0'
}

version = "${project_version}" + (isSnapshot() ? '+' + getBuildNumber() : '')

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation group: 'com.diluv.confluencia', name: 'Confluencia', version: '0.7.0'
    implementation group: 'com.diluv.clamchowder', name: 'Clam-Chowder', version: '1.0.0'

    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.14'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.6.0'
}

test {
    useJUnitPlatform()
}

jib {
    to {
        image = "diluv/hilo:" + version
        tags = ['latest' + (isSnapshot() ? "-snapshot" : '')]
        auth {
            username = System.getenv("DOCKER_USERNAME")
            password = System.getenv("DOCKER_PASSWORD")
        }
    }
}

static String getBuildNumber() {
    return System.getenv("BUILD_NUMBER") ?: "0"
}

static boolean isSnapshot() {
    String ref = System.getenv("GITHUB_REF");
    if (ref != null && ref.startsWith("refs/tags/v")) {
        return false
    }
    return true
}

jar {
    manifest {
        attributes([
            'Timestamp': System.currentTimeMillis(),
            'Specification-Title': project.archivesBaseName,
            'Specification-Vendor': project.vendor,
            'Specification-Version': project.version,
            'Implementation-Title': project.archivesBaseName,
            'Implementation-Version': project.version,
            'Implementation-Vendor' : project.vendor,
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'Built-On-Java': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
        ])
    }
}